/*************************
Authors: Bar Ben-zvi, Christian Yan, Tsering Ngoche, Nicole Smith
Date Created: 5/20/19
Brief: Shoots a projectile and creates aiming displays
*************************/
class ShootVariables
{
  //Weapon icon for display
  [Property]
  var Icon : SpriteSource = null;
  
  //Projectile archetype
  [Property]
  var Projectile : Archetype = null;
  
  //Amount of projectiles we will fire
  [Property]
  var ProjectileCount : Integer = 1;
  
  //This is in radians
  [Property]
  var SpreadCone : Real = Math.Pi / 4;
  
  //How fast the projectile will go when fired (not divided by DT)
  [Property]
  var ProjectileSpeed : Real = 100;
  
  //how much variation in speed you want for each bullet
  [Property]
  var SpeedVariation : Real = 0;
  
  //If you want the bullets to have variation in speed
  [Property]
  var SpeedBool : Boolean = false;
  
  [Property]
  var ShootSound : SoundCue = null;
}



class PlayerShooting : ZilchComponent
{
  //Starting weapon
  [Property]
  var Weapon : ShootVariables = ShootVariables();
  
  //Random functions
  var Random : Random = Random();
      
  //sound to play when weapon is switched
  [Property]
  var SwitchSound : SoundCue = null;
  
  [Property]
  var Active : Boolean = true;
  
  var CurrProjectile : Cog = null;
  
  //weapons inventory
  var Weapons : Array[ShootVariables] = Array[ShootVariables]();
  
  //weapon index
  var CurrWeapon : Integer = 0;

  //timer for the bullet
  var Timer : Real = 0;
  
  //This is the timer for when its switching turns
  var EndTimer : Real = 0;
  
  //turn this bool on for a timer to start
  var TBool : Boolean = false;
  
  //bool that should turn on when a bullet is fired
  var BBool : Boolean = false;
  
  //Max aim distance for the player
  [Property]
  var MaxAimDist : Real = 5.0;
  
  var WepCheat : Boolean = false;
  
  
  function Initialize(init : CogInitializer)
  {
    //Add the starting weapon to the inventory
    this.Weapons.Add(this.Weapon);
    
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    
    Zero.Connect(this.Space, Events.LeftMouseDown, this.OnLeftMouseDown);
    Zero.Connect(this.Space, Events.LeftMouseUp, this.OnLeftMouseUp);
    
    this.GameSession.FindSpaceByName("HUDSpace").FindObjectByName("LevelSettings").HUDManager.UpdateWeapons();

  }

  function OnLeftMouseDown(event : ViewportMouseEvent)
  {
    //create cast circle
    if(this.Active)
    {
      var object = this.Space.Create(Archetype.CastCircle);
      object.CursorFollow.RotateObject = this.Owner;
    }
  }

  function OnLeftMouseUp(event : ViewportMouseEvent)
  {
    //checks if we are currently allowed to fire
    if(this.Active)
    {
      //remove cast circle
      if(this.Space.FindObjectByName("CastCircle") != null)
      {
        this.Space.FindObjectByName("CastCircle").Destroy();
      }
      
      //gets the mouse position to know where to fire the bullet
      var mousePos = this.Space.FindObjectByName("GameCamera").CameraViewport.ScreenToWorldZPlane(event.Position, 0);
      //gets the direction of the mouse
      var dir = mousePos - this.Owner.Transform.WorldTranslation;

      var angleToMouse = Math.ATan2(dir.Y, dir.X);
      
      var normDir = Math.Normalize(Real3(dir.XY, 0)) * 2;
      
      var effect = this.Space.CreateAtPosition(Archetype.AttackEffect, this.Owner.Transform.WorldTranslation + normDir);
      
      effect.Transform.SetEulerAnglesXYZ(0, 0, angleToMouse - (Math.Pi / 2));
      
      //velocity based on mouse dist
      var power = 1.0;
      if(CustomMath.Magnitude(dir) < this.MaxAimDist)
      {
        power = CustomMath.Magnitude(dir) / this.MaxAimDist;
      }
      this.Space.SoundSpace.PlayCue(this.Weapon.ShootSound);
      if(this.Weapon.Projectile != null)
      {

        Console.WriteLine("Happened");
        //loop for how many bullets you want fired
        for(var i = 0; i < this.Weapon.ProjectileCount; ++i)
        {
          var randVel : Real = 1;
          if(this.Weapon.SpeedBool)
          {
            randVel = this.Random.Range(1,this.Weapon.SpeedVariation);
          }
          //create the projectile at the position
          this.CurrProjectile = this.Space.CreateAtPosition(this.Weapon.Projectile, this.Owner.Transform.WorldTranslation);
          //Determine the angle 
          var currAngle = angleToMouse + this.Random.Range(-this.Weapon.SpreadCone, this.Weapon.SpreadCone);
          //this giving the bullet velocity and what direction
          this.CurrProjectile.RigidBody.Velocity = CustomMath.DirectionFromAngle(currAngle) * this.Weapon.ProjectileSpeed * randVel * power;
          //Tells the camera to follow 
          //this.Space.FindObjectByName("GameCamera").CameraFollow.Target = this.CurrProjectile;
          //this turns on the bool so that the current projectile will be tracked
          this.BBool = true;
          this.GameSession.GameVars.BulletArray.Add(this.CurrProjectile);
        }
        //this sets the active bool to false so that you can not shoot again this phase
        this.Active = false;
      }
      
    }
    
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    if(this.Active)
    {
      //go left in weapon array
      if(Zero.Keyboard.KeyIsPressed(Keys.Q))
      {
        if(this.Weapons.Count != 1)
        {
          this.Space.SoundSpace.PlayCue(this.SwitchSound);
        }
        --this.CurrWeapon;
        //wrap to right if out of bounds
        if(this.CurrWeapon < 0)
        {
          this.CurrWeapon = this.Weapons.Count -1;
        }
        this.Weapon = this.Weapons[this.CurrWeapon];
        
        this.GameSession.FindSpaceByName("HUDSpace").FindObjectByName("LevelSettings").HUDManager.UpdateWeapons();
        
      }
      //go right in weapon array
      if(Zero.Keyboard.KeyIsPressed(Keys.E))
      {
        if(this.Weapons.Count != 1)
        {
          this.Space.SoundSpace.PlayCue(this.SwitchSound);
        }
        
        ++this.CurrWeapon;
        //wrap to left if out of bounds
        if(this.CurrWeapon >= this.Weapons.Count)
        {
          this.CurrWeapon = 0;
        }
        this.Weapon = this.Weapons[this.CurrWeapon];
        
        this.GameSession.FindSpaceByName("HUDSpace").FindObjectByName("LevelSettings").HUDManager.UpdateWeapons();
      }
      //draw aiming line
      if(Zero.Mouse.IsButtonDown(MouseButtons.Left))
      {
        var mousePos = Zero.Mouse.ClientPosition;
        var screenMousePos = this.Space.FindObjectByName("GameCamera").CameraViewport.ScreenToWorldZPlane(mousePos, 0);
        
        var distToMouse = screenMousePos - this.Owner.Transform.WorldTranslation;
        var dirToAngle = Math.Normalize(distToMouse);
        var angleToMouse = Math.ATan2(dirToAngle.Y, dirToAngle.X);
        var dirToMouse = CustomMath.DirectionFromAngle(angleToMouse);
        
        if(CustomMath.Magnitude(distToMouse) > this.MaxAimDist)
        {
          distToMouse = Math.Normalize(distToMouse) * this.MaxAimDist;
        }
        
        var debugLine = DebugLine(this.Owner.Transform.WorldTranslation, this.Owner.Transform.WorldTranslation + distToMouse);
        
        debugLine.Color = Real4(1, 1, 1, 1);
        
        DebugDraw.Add(debugLine);
        
        if(screenMousePos.X > this.Owner.Transform.WorldTranslation.X)
        {
          Console.WriteLine("right");
          this.Owner.Sprite.FlipX = false;
        }
        else if(screenMousePos.X < this.Owner.Transform.WorldTranslation.X)
        {
          Console.WriteLine("left");
          this.Owner.Sprite.FlipX = true;
        }
      }
      
      if(Zero.Keyboard.KeyIsPressed(Keys.K))
      {
        this.BBool = true;
        this.Active = false;
        this.Timer = 2.9;
      }
      
      //Weapon cheat (can only be used once per player)
      if(Zero.Keyboard.KeyIsDown(Keys.W) && Zero.Keyboard.KeyIsDown(Keys.E) && Zero.Keyboard.KeyIsDown(Keys.P) && !this.WepCheat)
      {
        this.WepCheat = true;
        var pos = this.Owner.Transform.WorldTranslation;
        
        this.Space.CreateAtPosition(Archetype.PlantWeaponPickup, pos);
        this.Space.CreateAtPosition(Archetype.LobWeaponPickup, pos);
        this.Space.CreateAtPosition(Archetype.ExplosionWeaponPickup, pos);
        this.Space.CreateAtPosition(Archetype.ClusterWeaponPickup, pos);
        
      }
      
    }
    
    //toggle this if you want a timer
    if(this.TBool)
    {
        this.Timer += event.Dt;
    }
    
    //this is for switchign after the bullet gets destroyed
    if(this.BBool && this.GameSession.GameVars.BulletArray.Count == 0)
    {
        //Add to the timer 
        this.EndTimer += event.Dt;
        
        if(this.EndTimer >= 1)
        {
          //changes whos turn it is
          this.LevelSettings.PlayerTurnManager.TurnBool = true;
          //turn off the bools
          this.BBool = false;
        }
    }
  }
  
  function Activate()
  {
    this.Active = true;
    if(Zero.Mouse.IsButtonDown(MouseButtons.Left))
    {
      var object = this.Space.Create(Archetype.CastCircle);
      object.CursorFollow.RotateObject = this.Owner;
    }
  }
  
}
