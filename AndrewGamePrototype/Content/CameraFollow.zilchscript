class CameraFollow : ZilchComponent
{
  //between 0 and 1
  [Property]
  var LerpSpeed : Real = 0.25;
  
  [Property]
  var ResizeSpeed : Real = 1.5;
  
  [Property]
  var MinMaxSize : Real2 = Real2(5, 40);
  
  //this is for when the camera is following spread and what the default size should be when adjusting to the spread
  [Property]
  var MinCamSize : Real = 20;
  
  //target the camera will move to
  var Target : Cog = null;
  
  var Total : Real3 = Real3(0,0,10);
  
  var CurrentSize : Real;
  
  function Initialize(init : CogInitializer)
  {
    //for testing
    //this.Target = this.Space.FindObjectByName("Player");
    this.CurrentSize = this.Owner.Camera.Size;
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    
    Zero.Connect(this.Space, Events.MouseScroll, this.OnMouseScroll);
  }

  function OnMouseScroll(event : ViewportMouseEvent)
  {
    this.CurrentSize += -event.Scroll.Y * this.ResizeSpeed;
    
    this.CurrentSize= Math.Clamp(this.CurrentSize, this.MinMaxSize.X, this.MinMaxSize.Y);
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    Console.WriteLine(this.Owner.Transform.Translation);
    var i : Integer = 0;
  
    //loop through the array
    for(i; i < this.GameSession.GameVars.BulletArray.Count; ++i)
    {
      //get rid of any nulls
      if(this.GameSession.GameVars.BulletArray[i] == null)
      {
        this.GameSession.GameVars.BulletArray.RemoveAt(i);
      }
    }
    if(this.GameSession.GameVars.BulletArray.Count != 0)
    {
      this.Total = Real3(0,0,10);
      this.CameraPosition();
    }
    else
    {
      this.Owner.Camera.Size = this.CurrentSize;
      //If our target exists
      if(this.Target != null)
      {
        var currentTranslation = this.Owner.Transform.WorldTranslation;
        var targetTranslation = Real3(this.Target.Transform.WorldTranslation.XY, currentTranslation.Z);
        
        this.Owner.Transform.WorldTranslation = Math.Lerp(currentTranslation, targetTranslation, this.LerpSpeed);
      }
    }
  }
  
  //this function gets where the camera should be
  function CameraPosition()
  {
    var farX : Real = 0;
    var farY : Real = 0;
    //find the lowest and highest x and y values
    foreach(var x : Cog in this.GameSession.GameVars.BulletArray)
    {
      if(x != null)
      {
        //this is to get the farthest point from the camera in order to resize the camera to fit all of the bullets
        if(x.Transform.Translation.X > farX)
        {
          farX = x.Transform.Translation.X;
        }
        if(x.Transform.Translation.Y > farY)
        {
          farY = x.Transform.Translation.Y;
        }
        this.Total.X += x.Transform.Translation.X;
        this.Total.Y += x.Transform.Translation.Y;
      }
    }
    
    //get the middle of the x and y
    this.Total.XY /= this.GameSession.GameVars.BulletArray.Count;
    var currentTranslation = this.Owner.Transform.WorldTranslation;
    var targetTranslation = Real3(this.Total.XY, this.Total.Z);
    //find the greatest distane from the two poitns and set the camera size to that
    this.Owner.Transform.WorldTranslation = Math.Lerp(currentTranslation, targetTranslation, this.LerpSpeed);
    if(Math.Abs(farX) > Math.Abs(farY) && this.GameSession.GameVars.BulletArray.Count != 0)
    {
      this.Owner.Camera.Size = Math.Abs(this.Owner.Transform.Translation.X - farX) + this.MinCamSize;
    }
    else
    {
      this.Owner.Camera.Size = Math.Abs(this.Owner.Transform.Translation.Y - farY) + this.MinCamSize;
    }
  }
}
