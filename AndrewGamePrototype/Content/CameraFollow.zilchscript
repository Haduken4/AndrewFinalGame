class CameraFollow : ZilchComponent
{
  //between 0 and 1
  [Property]
  var LerpSpeed : Real = 0.25;
  
  [Property]
  var ResizeSpeed : Real = 1.5;
  
  [Property]
  var MinMaxSize : Real2 = Real2(5, 40);
  
  //target the camera will move to
  var Target : Cog = null;
  
  
  
  function Initialize(init : CogInitializer)
  {
    //for testing
    //this.Target = this.Space.FindObjectByName("Player");
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    
    Zero.Connect(this.Space, Events.MouseScroll, this.OnMouseScroll);
  }

  function OnMouseScroll(event : ViewportMouseEvent)
  {
    this.Owner.Camera.Size += -event.Scroll.Y * this.ResizeSpeed;
    
    this.Owner.Camera.Size = Math.Clamp(this.Owner.Camera.Size, this.MinMaxSize.X, this.MinMaxSize.Y);
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    //If our target exists
    if(this.Target != null)
    {
      var currentTranslation = this.Owner.Transform.WorldTranslation;
      var targetTranslation = Real3(this.Target.Transform.WorldTranslation.XY, currentTranslation.Z);
      
      this.Owner.Transform.WorldTranslation = Math.Lerp(currentTranslation, targetTranslation, this.LerpSpeed);
    }
  }
}
